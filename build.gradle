buildscript {
  apply from: "version.gradle"
  addRepos(repositories)
  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.gradle_bintray_plugin
    classpath deps.android_maven_gradle_plugin
  }
}

allprojects {
  addRepos(repositories)
}

subprojects {
    def isAppModule = it.name == "app"
    apply plugin: isAppModule ? 'com.android.application' : 'com.android.library'
    android {
        compileSdkVersion build_versions.compile_sdk
        defaultConfig {
            if (isAppModule) applicationId "com.lzy.imagepickerdemo"
            minSdkVersion build_versions.min_sdk
            targetSdkVersion build_versions.target_sdk

            versionCode build_versions.androidVersionCode
            versionName build_versions.androidVersionName
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            multiDexEnabled true
        }
        lintOptions {
            abortOnError false
        }
    }
}
task clean(type: Delete) {
  delete rootProject.buildDir
}
